# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# load .profile (env)
[[ -e ~/.profile ]] && emulate sh -c 'source ~/.profile'

# Enable colors and change prompt:
autoload -U colors && colors
eval "$(dircolors -b)"

# base16
if [ -f ~/.config/local/base16.sh ]; then
  source ~/.config/local/base16.sh
fi

# Keep 1000 lines of history within the shell 
setopt histignorealldups sharehistory
HISTSIZE=1000
SAVEHIST=1000

# Basic auto/tab complete:
autoload -U compinit
zstyle ':completion:*' menu select
# Auto complete with case insenstivity
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zmodload zsh/complist
compinit
_comp_options+=(globdots)   # Include hidden files.

# Use vi keybindings even if our EDITOR is set to vi
bindkey -v
export KEYTIMEOUT=1

# Use vim keys in tab complete menu:
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'j' vi-down-line-or-history
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'left' vi-backward-char
bindkey -M menuselect 'down' vi-down-line-or-history
bindkey -M menuselect 'up' vi-up-line-or-history
bindkey -M menuselect 'right' vi-forward-char
# Fix backspace bug when switching modes
bindkey "^?" backward-delete-char

# Change cursor shape for different vi modes.
function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then
    echo -ne '\e[1 q'
  elif [[ ${KEYMAP} == main ]] ||
       [[ ${KEYMAP} == viins ]] ||
       [[ ${KEYMAP} = '' ]] ||
       [[ $1 = 'beam' ]]; then
    echo -ne '\e[3 q'
  fi
}
zle -N zle-keymap-select

zle-line-init() {
  zle -K viins # initiate `vi insert` as keymap (can be removed if `bindkey -V` has been set elsewhere)
  echo -ne "\e[3 q"
}
zle -N zle-line-init

echo -ne '\e[3 q' # Use bar shape cursor on startup.
preexec() { echo -ne '\e[3 q' ;} # Use bar shape cursor for each new prompt.

# Edit line in vim with ctrl-e:
autoload edit-command-line; zle -N edit-command-line
bindkey '^v' edit-command-line

fpath=($fpath "/home/lgranie/.zfunctions")

# Set up the prompt
# Set Spaceship ZSH as a prompt
SPACESHIP_PROMPT_ADD_NEWLINE=false
SPACESHIP_PROMPT_SEPARATE_LINE=true
SPACESHIP_CHAR_SYMBOL=â¯
SPACESHIP_CHAR_SUFFIX=" "
autoload -Uz promptinit
promptinit
prompt spaceship
spaceship_vi_mode_disable

# use fzf bindings
if [ -f /usr/share/doc/fzf/examples/key-bindings.zsh ]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh
fi 

# Load zsh-syntax-highlighting; should be last.
if [ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
  source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh 2>/dev/null
fi

if [ -f ~/.aliases ]; then
  source ~/.aliases
fi

